import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'

ext {
    major = 0
    minor = 0
    patch = 0

    adapterName = rootProject.name
    replacements = [
            adapterName: adapterName,
    ]
}

version = major + "." + minor + "." + patch

repositories {
    mavenCentral()
    maven {
        //TODO: change to public facing client artifactory
        url "http://artifactory.caplin.com:8081/artifactory/caplin-release/"
    }
}

dependencies {
    compile group: 'com.caplin.platform.components.datasource', name: 'datasource-java', version: '6.2.+', classifier: 'jar-with-dependencies', {
        transitive = false
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.caplin.template.AdapterTemplate',
                "Class-Path": configurations.compile.collect { it.getName() }.join(' ')
        )
    }
}

task createKit(type: Zip, dependsOn: jar) {
    archiveName = adapterName + "-" + version + ".zip"
    def topDirName = archiveName.substring(0, archiveName.lastIndexOf("."))

    into("${topDirName}") {
        from "${project.projectDir}/blade"
        include 'blade_config/bootstrap.conf'
        include 'DataSource/bin/start-jar.sh'
        filter(ReplaceTokens, tokens: replacements)
    }

    into("${topDirName}") {
        from "${project.projectDir}/blade"
        exclude 'blade_config/bootstrap.conf'
        exclude 'DataSource/bin/start-jar.sh'
    }

    into("${topDirName}/DataSource/lib") {
        from project.jar.archivePath
    }

    into("${topDirName}/DataSource/lib") {
        from configurations.compile
    }
}

assemble.dependsOn(createKit)